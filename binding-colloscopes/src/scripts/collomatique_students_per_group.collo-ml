import "collomatique_vars" as *;

let count_students_in_group(group: Group) -> LinExpr =
    if group.group_list.prefilled {
        LinExpr(|group.prefilled_students|)
    } else {
        sum student in group.group_list.students {
            $StudentInGroup(student, group)
        }
    };

let enforce_min_students_per_group(group: Group) -> Constraint =
    count_students_in_group(group) >==
        group.group_list.min_students_per_group * $GroupHasStudents(group);

let enforce_max_students_per_group(group: Group) -> Constraint =
    count_students_in_group(group) <== group.group_list.max_students_per_group;

pub let constraint() -> Constraint =
    forall group in @[Group] {
        enforce_min_students_per_group(group) and
        enforce_max_students_per_group(group)
    };
