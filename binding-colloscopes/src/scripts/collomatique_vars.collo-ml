let count_groups_in_interrogation(interrogation: Interrogation) -> LinExpr =
    match interrogation.group_list {
        gl as GroupList {
            sum group in gl.groups {
                $GroupInInterrogation(interrogation, group.num)
            }
        }
        _ as None {
            LinExpr(0)
        }
    };

let interrogation_has_groups(interrogation: Interrogation) -> Constraint =
    count_groups_in_interrogation(interrogation) >== 1;

pub reify interrogation_has_groups as $InterrogationHasGroups;

let student_in_group(student: Student, group: Group) -> Constraint =
    $StudentGroup(student, group.group_list) === group.num;

pub reify student_in_group as $StudentInGroup;

let group_has_students(group: Group) -> Constraint =
    if group.group_list.prefilled {
        |group.prefilled_students| >== 1
    } else {
        sum student in group.group_list.students {
            $StudentInGroup(student, group)
        } >== 1
    };

pub reify group_has_students as $GroupHasStudents;

let student_at_interrogation_in_group(student: Student, interrogation: Interrogation, group: Group) -> Constraint =
    // Use inequalities to reduce the amount of helper variables
    $StudentInGroup(student, group) >== 1 and $GroupInInterrogation(interrogation, group.num) >== 1;

pub reify student_at_interrogation_in_group as $StudentAtInterrogationInGroup;

let student_prefilled_group(student: Student, group_list: GroupList) -> Int =
    // returns -1 if no group corresponds
    fold group in group_list.groups with result = -1 {
        /* with a consistent database only one such group might exists
           but in principle this code returns the last one found */
        if student in group.prefilled_students {
            group.num
        } else {
            result
        }
    };

let student_at_interrogation(student: Student, interrogation: Interrogation) -> Constraint =
    match interrogation.group_list {
        gl as GroupList {
            if gl.prefilled {
                $GroupInInterrogation(
                    interrogation,
                    student_prefilled_group(student, gl)
                )
            } else {
                sum group in gl.groups {
                    $StudentAtInterrogationInGroup(student, interrogation, group)
                }
            }
        }
        _ as None {
            0
        }
    } >== 1; // Test for bigger than one: we can't actually exceed it but this will save some reification constraints

pub reify student_at_interrogation as $StudentAtInterrogation;

